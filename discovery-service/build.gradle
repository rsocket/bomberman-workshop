import io.netifi.flatbuffers.plugin.tasks.FlatBuffers

plugins {
    id "java"
    id "io.netifi.flatbuffers"
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

configurations {
    flatbuf
}

dependencies {
    flatbuf(project(":discovery-service-idl"))

    implementation platform("io.rsocket:rsocket-bom:1.1.0")

    implementation("io.rsocket:rsocket-core")
    implementation("io.rsocket:rsocket-transport-netty")

    compileOnly("org.projectlombok:lombok:1.18.20")
    annotationProcessor("org.projectlombok:lombok:1.18.20")

    testImplementation("junit:junit:4.12")
}

flatbuffers {
    flatBuffersVersion = '1.12.0'
}

processResources {
    dependsOn configurations.flatbuf
    configurations.flatbuf.files.each {
        from(zipTree(it).matching {
            include "*.fbs"
        })
    }
}

task createFlatBuffers(type: FlatBuffers, dependsOn: ['processResources']) {
    language = 'java'
    inputDir = file("$buildDir/resources/main")
    outputDir = file("$buildDir/generated/sources/flatbuffers/java/main")
    extraArgs = "--gen-mutable"
}

compileJava {
    dependsOn createFlatBuffers
}
